# nixos terminal login does not do this
# for reasons which escape me.
if [[ -z $SOURCED_PROFILE ]] && [[ -f "$HOME/profile" ]]; then
    source $HOME/.profile
fi

eval "$(direnv hook zsh)"

ZSH=~/.zsh

[[ $TERM == "dumb" ]] && unsetopt zle && PS1='$ ' && return

if [[ -f ${ZSH}/${HOST}.zsh ]]; then
    source ${ZSH}/${HOST}.zsh
fi

# include path

fpath=( $ZSH/functions "${fpath[@]}" )
autoload -Uz $ZSH/functions/*(:t)

## History
setopt appendhistory
setopt hist_ignore_space
setopt extendedhistory
setopt incappendhistory

if [ "$HISTFILE" = "$HOME/.zsh_history" ]; then
    HISTFILE=~/.zsh/history
fi
HISTSIZE=10000
SAVEHIST=10000

source ${ZSH}/zsh-histdb/sqlite-history.zsh
source ${ZSH}/zsh-histdb/histdb-interactive.zsh

export ALTERNATE_EDITOR=""
export EDITOR="emacsclient"
export VISUAL="emacsclient -c"

unsetopt share_history
setopt no_share_history

whence gpg-connect-agent >/dev/null &&
    gpg-connect-agent -q updatestartuptty /bye 2>&1 >/dev/null

set -K # disable stupid exclamation mark

# options
setopt autocd nobeep extendedglob nomatch prompt_subst re_match_pcre
setopt longlistjobs checkjobs noflowcontrol
unsetopt notify

autoload -U zutil
autoload -U zmv

autoload compinit
autoload -U complist
compinit

autoload -U colors && colors

setopt complete_in_word nolistambiguous
setopt menu_complete

setopt magic_equal_subst

zstyle ':completion:*' rehash true

zstyle ':completion:*' completer _complete _ignored
zstyle -e ':completion:*:approximate:*' max-errors 'reply=($((($#PREFIX+$#SUFFIX)/3)) numeric)'
zstyle ':completion:*' add-space true
zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}'
zstyle ':completion:*:*:*:*:*' menu select=2
zstyle ':completion:*:default' list-colors ''
# match from middle of strings?
zstyle ':completion:*' matcher-list '' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}' '+l:|=* r:|=*'
zstyle ':completion:*:functions' ignored-patterns '(_*|pre(cmd|exec))'

zstyle ':completion:*:matches' group 'yes'
zstyle ':completion:*:descriptions' format "%{${fg_bold[magenta]}%}%d:%{$reset_color%}"
zstyle ':completion:*' group-name ""
zstyle ':completion:*' accept-exact '*(N)'

zstyle ':completion:*:manuals' separate-sections true
zstyle ':completion:*:manuals.(^1*)' insert-sections true

zstyle :compinstall filename '/home/hinton/.zshrc'

_my_cd () {
    _cd
    _my_files    
}

compdef '_my_cd' cd

_my_cp () {
    _cp
    _my_files
}

compdef '_my_cp' cp

bindkey '^r' _histdb-isearch

# prevent deleting entire paths
WORDCHARS='*?[]~=&;!#$%^(){}<>'

# prompt garble
RPROMPT=''
case $TERM in
    dumb)
        PROMPT='%~ %(?,,[%?] )$ '
    ;;
    *)
        PROMPT=''
        if [[ -n "$SSH_CLIENT" ]]; then
            PROMPT=$PROMPT'%F{cyan}%U%m%u%f '
        fi
        if [[ ! -z $IN_NIX_SHELL ]]; then
            PROMPT=$PROMPT'N '
        fi
        PROMPT=$PROMPT'%B%~%b'"\${vcs_info_msg_0_}
%B%(?,,%F{magenta}[%?]%f )$%b "
        ;;
esac

autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git svn hg
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' formats " %F{cyan}%b%c%u%f"
zstyle ':vcs_info:git*:*' unstagedstr "%F{red}?%f"
zstyle ':vcs_info:git*:*' stagedstr "%F{green}+%f"
zstyle ':vcs_info:*' disable-patterns "$HOME/net(|/*)" "/net(|/*)"

precmd() {
    vcs_info
    if    ls -1qA ~/.msmtp.queue/ | grep -q .; then
        echo "unsent mails..."
    fi
}

preexec() {

}

autoload -Uz add-zsh-hook

add-zsh-hook preexec _start_timer
add-zsh-hook precmd _stop_timer
add-zsh-hook precmd _set_title
add-zsh-hook preexec _set_title
add-zsh-hook precmd histdb-update-outcome

if [ -n "$TMUX" ]; then
    refresh-environment () {
        export $(tmux show-environment | grep '^\(SSH_AUTH_SOCK\|DISPLAY\)') >/dev/null
    }

    add-zsh-hook preexec refresh-environment
fi

autoload edit-command-line
zle -N edit-command-line
bindkey "^X^E" edit-command-line

# command aliases
alias ls='TIME_STYLE=long-iso BLOCK_SIZE=1 ls -h --color=auto -v'
alias l='ls -l'
alias la='l -a'
alias ecn='emacsclient -c -n'
alias grep='grep --color=auto'
alias mkpatch='diff -Naur'
alias cal='cal -yw --color'
alias mkdir='mkdir -p -v'
alias copy='rsync -a --info=progress2 --no-i-r -h'
alias pgrep='pgrep -a'
alias nn='notmuch new'
alias serve='webfsd -F -l -'

export LESS='--quit-if-one-screen --ignore-case --status-column --LONG-PROMPT --tabs=4 --no-init -R'

lein() {
    case $1 in
        figwheel)
            command rlwrap lein "$@"
            ;;
        *)
            command lein "$@"
            ;;
    esac
}

alias javadebug='java -agentlib:jdwp=transport=dt_socket,server=y,address=8000,suspend=y'

alias -s org=xdg-open
alias -s pdf=xdg-open
alias -s jpg=xdg-open
alias -s png=xdg-open

MARKPATH=$ZSH/run/marks
bookmark -l

# directory memory
autoload -Uz chpwd_recent_dirs cdr
add-zsh-hook chpwd chpwd_recent_dirs

zstyle ':chpwd:*' recent-dirs-default yes
zstyle ':chpwd:*' recent-dirs-max 50
zstyle ':completion:*' recent-dirs-insert always

goBack () {
    popd >/dev/null
    zle reset-prompt
}

goUp () {
    cd ..>/dev/null
    zle reset-prompt
}

zle -N goUp
bindkey '^[u' goUp
zle -N goBack
bindkey '^[i' goBack

DIRSTACKSIZE=8
setopt autopushd pushdminus pushdsilent pushdtohome

zle -N _accept_or_ls

zle -N _dired

bindkey "^M" _accept_or_ls

autoload -Uz copy-earlier-word
zle -N copy-earlier-word
bindkey "^[m" copy-earlier-word

bindkey '^Q' push-line-or-edit

alias edit="emacsclient -c"

alias -g ND='*(/om[1])' # newest directory
alias -g NF='*(.om[1])' # newest file
alias -g SP='$(shelf)' # last shelfd file

compdef _gnu_generic msmtp-queue

unalias run-help
run-help () {
    man "$@" || \ "$1" --help | less
}
zmodload zsh/complist
bindkey -M menuselect '^o' accept-and-infer-next-history

eval "$(direnv hook zsh)"

# Local Variables:
# mode: sh
# End:

