# nixos terminal login does not do this
# for reasons which escape me.
if [[ -z $SOURCED_PROFILE ]] && [[ -f "$HOME/profile" ]]; then
    source $HOME/.profile
fi

# include path
ZSH=~/.zsh
fpath=( $ZSH/functions "${fpath[@]}" )
autoload -Uz $ZSH/functions/*(:t)

# options
setopt autocd nobeep extendedglob nomatch prompt_subst menu_complete re_match_pcre
setopt longlistjobs checkjobs
unsetopt notify

autoload -U zutil

autoload compinit
autoload -U complist
compinit

autoload -U colors && colors

# completion settings
zstyle -e ':completion:*' completer '
  if [[ $_last_try != "$HISTNO$BUFFER$CURSOR" ]]; then
    _last_try="$HISTNO$BUFFER$CURSOR"
    reply=(_complete _match _prefix)
  else
    reply=(_ignored _correct _approximate)
  fi'

zstyle ':completion:*' max-errors 1
zstyle ':completion:*' file-sort name
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more%s
zstyle ':completion:*' matcher-list '' 'm:{[:lower:]}={[:upper:]}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
zstyle ':completion:*' menu select=1 _complete _ignored _approximate
zstyle ':completion:*' select-prompt %Sscrolled %p%s
zstyle ':completion:*' verbose true
zstyle ':completion:*:descriptions' format '%B%F{blue}%d%f%b'
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#) ([0-9a-z-]#)*=01;34=0=01'
zstyle ':completion:*:kill:*' force-list always
zstyle ':completion:*:processes' command "ps -eo pid,user,comm,cmd -w -w"
zstyle ':completion:*:functions' ignored-patterns '_*'
# not sure about this
zstyle ':completion:*:default' list-colors \
       ${(s.:.)LS_COLORS}
# this is bad.
#zstyle ':completion:*' recursive-files '*/.git/*'
zstyle ':completion:*' separate-sections yes

zstyle :compinstall filename '/home/hinton/.zshrc'

## History
setopt appendhistory
setopt hist_ignore_space
setopt extendedhistory
setopt incappendhistory

HISTFILE=~/.zsh/history
HISTSIZE=10000
SAVEHIST=10000

if whence hstr >/dev/null; then
    export HH_CONFIG=hicolor        # get more colors
    bindkey -s "\C-h" "\eqhh\n"
fi

# (( $+widgets[history-incremental-pattern-search-backward] )) &&	\
#     bindkey '^r' history-incremental-pattern-search-backward

function history-incremental-search-backward-buffer () {
    if [[ -n "$BUFFER" ]]; then
        zle -U  # this is mad but it works.
        zle history-incremental-search-backward "$BUFFER"
    else
        zle history-incremental-search-backward
    fi
}

zle -N history-incremental-search-backward-buffer
bindkey '^r' history-incremental-search-backward-buffer
bindkey -M isearch '^r' history-incremental-search-backward
# repair C-r insertion

# prevent deleting entire paths
WORDCHARS='*?_-.[]~=&;!#$%^(){}<>'

# prompt garble
RPROMPT=''
case $TERM in
    dumb)
        PROMPT='%~ %(?,,[%?] )$ '
    ;;
    *)
        PROMPT=''
        if [[ -n "$SSH_CLIENT" ]]; then
            PROMPT=$PROMPT'%F{cyan}%U%m%u%f '
        fi
        if [[ ! -z $IN_NIX_SHELL ]]; then
            PROMPT=$PROMPT'N '
        fi
        PROMPT=$PROMPT'%B%~%b'"\${vcs_info_msg_0_}
%B%(?,,%F{red}[%?]%f )$%b "
        ;;
esac

autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git svn hg
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' formats " %F{white}%K{black}%b%c%u%k%f"
zstyle ':vcs_info:git*:*' unstagedstr "%F{red}?%f"
zstyle ':vcs_info:git*:*' stagedstr "%F{green}+%f"

precmd() {
    vcs_info
}

preexec() {

}

autoload -Uz add-zsh-hook

add-zsh-hook preexec _start_timer
add-zsh-hook precmd _stop_timer
add-zsh-hook precmd _set_title
add-zsh-hook preexec _set_title

if [[ -n $DISPLAY ]]; then
    add-zsh-hook precmd _xprop_update
fi

case $TERM in
    eterm*)
        if [[ -d $ZSH/eterm ]]; then
            fpath=( $ZSH/eterm "${fpath[@]}" )
            autoload -Uz $ZSH/eterm/*(:t)
            add-zsh-hook precmd _eterm_update
            _eterm_update
        fi
        ;;
esac

autoload edit-command-line
zle -N edit-command-line
bindkey "^X^E" edit-command-line

# command aliases
alias ls='ls -h --color'
alias l='ls -l'
alias la='l -a'
alias add='paste -s -d+|bc'
alias tab='cut -d "	" -f '
alias ecn='emacsclient -c -n'

alias javadebug='java -agentlib:jdwp=transport=dt_socket,server=y,address=8000,suspend=y'

o() {
    xdg-open "$@" &|
}

alias -s org=o
alias -s pdf=o
alias -s jpg=o
alias -s png=o

MARKPATH=$ZSH/run/marks
bookmark -l

# directory memory
autoload -Uz chpwd_recent_dirs cdr
add-zsh-hook chpwd chpwd_recent_dirs

zstyle ':chpwd:*' recent-dirs-default yes
zstyle ':chpwd:*' recent-dirs-max 50
zstyle ':completion:*' recent-dirs-insert always

rationalise-dot() {
  if [[ $LBUFFER = *.. ]]; then
    LBUFFER+=/..
  else
    LBUFFER+=.
  fi
#  zle reset-prompt works badly in eterm
}

zle -N rationalise-dot
bindkey . rationalise-dot
bindkey -M isearch . self-insert

DIRSTACKSIZE=8
setopt autopushd pushdminus pushdsilent pushdtohome

zle -N _accept_or_ls

_dired() {
    xdg-open . &|
}

zle -N _dired

bindkey "^M" _accept_or_ls
bindkey "^X^J" _dired

autoload -Uz copy-earlier-word
zle -N copy-earlier-word
bindkey "^[m" copy-earlier-word

_my_cd () {
    _cd
    _my_files
}

compdef '_my_cd' cd

_my_cp () {
    _cp
    _my_files
}

compdef '_my_cp' cp

alias edit="emacsclient -c"

export ALTERNATE_EDITOR=""
export EDITOR="emacsclient"
export VISUAL="emacsclient -c"

zle -C hist-complete complete-word _generic
zstyle ':completion:hist-complete:*' completer _history
bindkey "^X^R" hist-complete

alias -g ND='*(/om[1])' # newest directory
alias -g NF='*(.om[1])' # newest file

alias repl='rlwrap with '

whence gpg-connect-agent >/dev/null &&
    gpg-connect-agent -q updatestartuptty /bye 2>&1 >/dev/null

set -K # disable stupid exclamation mark

# Local Variables:
# mode: sh
# End:
